module core
use global refid 1000

type type builtin
type ref builtin
type bool builtin
type int builtin
type string builtin
type reflist builtin
type list reflist

any typeof(any i) {
  shell '''
  _wsh_get_local i _wshi
  _wsh_typeof "${_wshi}"
  _wsh_get_global "${_wsh_type}" _wsh_result
  '''
}

any _as(any i, any as) {
  in = typeof(i)
  shell '''
  _wsh_get_local in _wshi  # 'type/list:reflist'
  _wsh_get_local as _wshl  # 'type/reflist:builtin'

  case "${_wshl%%:*}" in type/*)
    :
  ;; *)
    _wsh_error="error:Expected a type (e.g. 'type/list:reflist'), got '${_wshl}'"
    return 1
  ;;
  esac

  _wshj="${_wshi#type/*:}" # 'reflist' - parent type (bare)
  _wshk="${_wshi%%:*}"
  _wshk="${_wshk#type/}"   # 'list' - own type (bare)

  _wshm="${_wshl#type/*:}" # 'builtin' - target type's parent
  _wshn="${_wshl%%:*}"
  _wshn="${_wshn#type/}"   # 'reflist' - target type (bare)
  _wsh_get_local i _wsho
  case "${_wshn}" in "${_wshk}")
    # target bare type is own bare type--noop
    _wsh_result="${_wsho}"
  ;; "${_wshj}")
    # target bare type is own parent bare type--unwrap
    _wsh_result="${_wsho#${_wshk}:}"
    _wsh_assert "${_wsh_result}" "${_wshn}" || return 1
  ;; *)
    case "${_wshk}" in "${_wshm}")
      # own bare type is target parent type--wrap
      _wsh_result="${_wshn}:${_wsho}"
    ;; *)
      _wsh_error="error:Can't cast ${_wshk} value to ${_wshn}"
      return 1
    ;;
    esac
  ;;
  esac
  '''
}

string _raw(any i) {
  shell '''
  _wsh_get_local i _wshi
  _wsh_result="string:${_wshi}"
  '''
}

ref new(type/ref t) {
  bool.new().ref()
}

ref ref(any i) {
  refid = refid + 1
  shell '''
  _wsh_get_local _reflist _wshrl
  _wsh_error="error:Couldn't create reference for value <<${_wshv_i}>>"
  _wsh_get_global refid _wshi
  _wsh_get_local i _wshr_${_wshi#int:}
  _wsh_error=error:
  _wsh_set_local r "ref:${_wshi#int:}"
  '''
  _reflist.push(r)
  r
}

any deref(ref r) {
  shell '''
  _wsh_get_local r _wshi
  _wsh_deref_into "${_wshi}" _wsh_result
  '''
}

bool eq(ref r, ref o) {
  shell '''
  _wsh_get_local r _wshi
  _wsh_get_local o _wshj
  case "${_wshi}" in "${_wshj}")
    _wsh_result=bool:true
  ;; *)
    _wsh_result=bool:false
  ;;
  esac
  '''
}

bool new(type/bool t) {
  false
}

bool as_bool(bool p) {
  p
}

int as_int(bool p) {
  if p {
    1
  } else {
    0
  }
}

bool not(bool p) {
  if p {
    false
  } else {
    true
  }
}

bool and(bool p, bool q) {
  _bool_arith_op(p.as_int(), q.as_int(), '&&')
}

bool or(bool p, bool q) {
  _bool_arith_op(p.as_int(), q.as_int(), '||')
}

bool eq(bool p, bool q) {
  shell '''
  _wsh_get_local p _wshi
  _wsh_get_local q _wshj
  case "${_wshi}" in "${_wshj}")
    _wsh_result=bool:true
  ;;
  *)
    _wsh_result=bool:false
  ;;
  esac
  '''
}

int new(type/int t) {
  0
}

int as_int(int i) {
  i
}

bool _bool_arith_op(int a, int b, string op) {
  shell '''
  _wsh_get_local a _wshi
  _wsh_get_local b _wshj
  _wsh_get_local op _wshk
  case "$((${_wshi#int:} ${_wshk#string:} ${_wshj#int:}))" in 1)
    _wsh_result=bool:true
  ;;
  *)
    _wsh_result=bool:false
  ;;
  esac
  '''
}

int _arith_op(int a, int b, string op) {
  shell '''
  _wsh_get_local a _wshi
  _wsh_get_local b _wshj
  _wsh_get_local op _wshk
  _wsh_result="int:$((${_wshi#int:} ${_wshk#string:} ${_wshj#int:}))"
  '''
}

bool eq(int a, int b) {
  _bool_arith_op(a, b, '==')
}

bool gt(int a, int b) {
  _bool_arith_op(a, b, '>')
}

bool lt(int a, int b) {
  _bool_arith_op(a, b, '<')
}

int plus(int a, int b) {
  _arith_op(a, b, '+')
}

int minus(int a, int b) {
  _arith_op(a, b, '-')
}

int times(int a, int b) {
  _arith_op(a, b, '*')
}

int div(int a, int b) {
  _arith_op(a, b, '/')
}

int mod(int a, int b) {
  _arith_op(a, b, '%')
}

string to_string(int i) {
  shell '''
  _wsh_get_local i _wshi
  _wsh_result="string:${_wshi#int:}"
  '''
}

string new(type/string t) {
  ''
}

string as_string(string s) {
  s
}

bool eq(string s, string o) {
  shell '''
  _wsh_get_local o _wshi
  _wsh_get_local s _wshj
  case "${_wshi}" in "${_wshj}")
    _wsh_result='bool:true'
  ;; *)
    _wsh_result='bool:false'
  ;;
  esac
  '''
}

string plus(string s, string o) {
  shell '''
  _wsh_get_local s _wshi
  _wsh_get_local o _wshj
  _wsh_result="${_wshi}${_wshj#string:}"
  '''
}

string times(string s, int i) {
  if i > 1 {
    s + times(s, i - 1)
  } else {
    s
  }
}

int length(string s) {
  shell '''
  _wsh_get_local s _wshi
  _wshj="${_wshi#string:}"
  _wsh_result="int:${#_wshj}"
  '''
}

string head(string s) {
  len = s.length()
  tail_len = len - 1
  pat = '?' * tail_len
  shell '''
  _wsh_get_local s _wshi
  _wsh_get_local pat _wshj
  _wshi="${_wshi#string:}"
  _wshj="${_wshj#string:}"
  _wsh_result="string:${_wshi%${_wshj}}"
  '''
}

string tail(string s) {
  shell '''
  _wsh_get_local s _wshi
  _wshi="${_wshi#string:}"
  _wsh_result="string:${_wshi#?}"
  '''
}

string at(string s, int i) {
  if i > 0 {
    at(s.tail(), i - 1)
  } else {
    s.head()
  }
}

reflist new(type/reflist t) {
  shell '''
  _wsh_result=reflist:
  '''
}

reflist push(reflist rl, ref r) {
  shell '''
  _wsh_get_local rl _wshi
  _wsh_get_local r _wshj
  case "${_wshi}" in reflist:)
    _wsh_result="${_wshi}${_wshj}"
  ;; *)
    _wsh_result="${_wshi} ${_wshj}"
  ;;
  esac
  '''
}

ref head(reflist rl) {
  shell '''
  _wsh_get_local rl _wshi
  case "${_wshi}" in reflist:)
    _wsh_error="error:attempted to take head() of []"
    return 1
  ;; *)
    _wshj="${_wshi#reflist:}"
    _wsh_result="${_wshj%% *}"
  ;;
  esac
  '''
}

reflist tail(reflist rl) {
  shell '''
  _wsh_get_local rl _wshi
  case "${_wshi}" in reflist:)
    _wsh_error="error:attempted to take tail() of []"
    return 1
  ;; *)
    _wshj="${_wshi#reflist:}"
    _wshk="${_wshj#* }"
    case "${_wshk}" in "${_wshj}")
      _wsh_result=reflist:
    ;; *)
      _wsh_result=reflist:"${_wshk}"
    ;;
    esac
  ;;
  esac
  '''
}

ref at(reflist rl, int i) {
  shell '''
  _wsh_get_local rl _wshi
  _wsh_get_local i _wshj
  _wshj="${_wshj#int:}"
  _wshm='notfound'
  : $((_wshk=0))
  for _wshl in ${_wshi#reflist:}
  do
    case $((_wshk == _wshj)) in 1)
      _wsh_result="${_wshl}"
      _wshm='found'
      break
    ;;
    esac
  done
  case "${_wshm}" in notfound)
    _wsh_error="error:array index out of bounds, ${_wshj} > ${_wshk}"
    return 1
  ;;
  esac
  '''
}

int length(reflist rl) {
  shell '''
  _wsh_get_local rl _wshi
  _wshk=0
  case "${_wshi}" in reflist:)
    _wsh_result='int:0'
  ;; *)
    for _wshj in ${_wshi#reflist:}
    do
      : $((_wshk++))
    done
    _wsh_result="int:${_wshk}"
  ;;
  esac
  '''
}

reflist minus(reflist rl, reflist o) {
  if o.length() > 0 {
    rl.delete(o.head()).minus(o.tail())
  } else {
    rl
  }
}

reflist delete(reflist rl, ref r) {
  _delete3(rl, r, reflist.new())
}

reflist _delete3(reflist rl, ref r, reflist out) {
  if rl.length() > 0 {
    if rl.head() == r {
      _delete3(rl.tail(), r, out)
    } else {
      _delete3(rl.tail(), r, out.push(r))
    }
  } else {
    out
  }
}

reflist set_push(reflist rl, ref r) {
  if !rl.has(r) {
    rl.push(r)
  } else {
    rl
  }
}

bool has(reflist rl, ref r) {
  shell '''
  _wsh_get_local rl _wshi
  _wsh_get_local r _wshj
  case "${_wshi#reflist:}" in *"${_wshj}"*)
    _wsh_result=bool:true
  ;; *)
    _wsh_result=bool:false
  ;;
  esac
  '''
}

string to_string(reflist rl) {
  shell '''
  _wsh_get_local rl _wshi
  _wsh_result="string:${_wshi}"
  '''
}

list as_list(reflist rl) {
  rl._as(list)
}

reflist as_reflist(list l) {
  l._as(reflist)
}

list as_list(list l) {
  l
}

list new(type/list t) {
  reflist.new().as_list()
}

list push(list l, any i) {
  l.as_reflist().push(i.ref()).as_list()
}

any head(list l) {
  l.as_reflist().head().deref()
}

list tail(list l) {
  l.as_reflist().tail().as_list()
}

any at(list l, int i) {
  l.as_reflist().at(i).deref()
}

int length(list l) {
  l.as_reflist().length()
}
