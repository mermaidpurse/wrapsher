module env

# This Source Code Form is subject to the terms of the Mozilla Public
# License, v. 2.0. If a copy of the MPL was not distributed with this
# file, You can obtain one at https://mozilla.org/MPL/2.0/.
#
# Copyright (c) 2025 Mermaidpurse

string _run_export(module/env m) {
  shell '''
  _wsh_result=string:"$(export -p)"
  '''
}

# TODO: filter valid lines
map to_map(module/env m) {
  out = env._run_export()
  convert = pair fun (string s) {
    shell '''
    _wsh_get_local s _wshi
    _wshi="${_wshi#string:}"
    _wshi="${_wshi#export }"
    _wshj="${_wshi#*=}"
    _wshi="${_wshi%%=*}"
    case "${_wshi}" in "${_wshj}")
      _wshj=''
    ;; *)
      _wshj="${_wshj%\"}"
      _wshj="${_wshj#\"}"
    ;;
    esac
    _wsh_makeref_into "string:${_wshi}" _wshk
    _wsh_makeref_into "string:${_wshj}" _wshl
    _wsh_result="pair+reflist:${_wshk} ${_wshl}"
    '''
  }
  lines = out.split('
')
  pl = lines.map(convert)
  # Unsafe, but okay because we constructed it above
  rv = pl._as(reflist)._as(map)
  rv
}

string at(module/env m, string k) {
  m.to_map().at(k)
}

string set(module/env m, pair p) {
  k = p.key()
  v = p.value()
  shell '''
  _wsh_get_local k _wshi
  _wsh_get_local v _wshj
  _wshj="${_wshj#string:}"
  eval "${_wshi#string:}=\"\${_wshj}\""
  export "${_wshi#string:}"
  _wsh_result="${_wshi}"
  '''
}

bool unset(module/env m, string k) {
  shell '''
  _wsh_get_local k _wshi
  unset "${_wshi#string:}"
  _wsh_result=bool:true
  '''
}
